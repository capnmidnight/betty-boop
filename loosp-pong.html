<!DOCTYPE html>
<html>
<head>
    <meta charset="UTF-8">
    <title>Ping Pong</title>
    <link type="text/css" rel="stylesheet" href="pong.css">
    <script type="text/javascript" src="loosp.js"></script>
    <script type="text/javascript" src="pon.js"></script>
    <script type="text/loosp">
        (define PLAY_WIDTH 800)
        (define PLAY_HEIGHT 400)
        (define LINE_WIDTH 10)
        (define PADDLE_LENGTH 100)
        (define AI_STEP 5)
        (define canv (document.getElementById "board"))
        (set! canv.width PLAY_WIDTH)
        (set! canv.height PLAY_HEIGHT)
        (set! canv.style.width (+ PLAY_WIDTH "px"))
        (set! canv.style.height (+ PLAY_HEIGHT "px"))
        (define g (canv.getContext "2d"))
        (define startButton (document.getElementById "start"))
        (define statusBox (document.getElementById "status"))
        (define p1 null)
        (define p2 null)
        (define ball null)
        (define timer null)
        (define lastFrame null)
        (define state null)

        (class (Rect x y width height)
            (set! this.x x)
            (set! this.y y)
            (set! this.width width)
            (set! this.height height)

            (method (draw)
                (g.fillRect this.x this.y this.width this.height)))

        (class (Player:Rect scoreDisplay x y width height)
            (base x y width height)
            (set! this.startY y)
            (set! this.score 0)
            (set! this.scoreDisplay scoreDisplay)

            (method (reset)
                (this.moveTo this.startY)
                (set! this.score 0)
                (set! this.scoreDisplay.innerHTML this.score)
                "Ok!")

            (method (moveTo y)
                (set! this.y y)
                (if (> this.y (- PLAY_HEIGHT PADDLE_LENGTH))
                    (set! this.y (- PLAY_HEIGHT PADDLE_LENGTH))
                    (when (< this.y 0)
                        (set! this.y 0))))

            (method (scored)
                (set! this.score (+ 1 this.score))
                (set! this.scoreDisplay.innerHTML this.score))

            (method (AI ball)
                (when (> ball.dx 0)
                    (if (> ball.y (- (+ this.y this.height) LINE_WIDTH))
                        (this.moveTo (+ 1 this.y))
                        (when (< ball.y this.y)
                              (this.moveTo (- this.y 1))))))

            (method (intersect ball)
                (and (>= (+ ball.x ball.width) this.x)
                     (< ball.x (+ this.x this.width))
                     (>= (+ ball.y ball.height) this.y)
                     (< ball.y (+ this.y this.height))))

            (method (bounce ball)
                (define good false)
                (when (this.intersect ball)
                    (set! ball.dx (* ball.dx -1.1))
                    (define dy (+ (- this.y ball.y) (* 0.5 (- this.height ball.height))))
                    (set! ball.dy (- ball.dy (* dy 0.003003003003003003003003003003)))
                    (set! good true))
                good))


        (class (Ball:Rect width height)
            (base -1000 0 width height)
            (set! this.dx 0)
            (set! this.dy 0)

            (method (drop direction speed)
                (set! this.x -100)
                (set! this.y 0)
                (set! this.x (* 0.5 (- PLAY_WIDTH LINE_WIDTH)))
                (set! this.y (* 0.5 (- PLAY_HEIGHT LINE_WIDTH)))
                (define vel (+ 0.2 (* 0.0375 speed)))
                (define angle (* (- (* (Math.random) 2) 1) Math.PI 0.25))
                (set! this.dx (* direction (Math.cos angle) vel))
                (set! this.dy (* (Math.sin angle) vel)))

            (method (advance)
                (set! this.x (+ this.x this.dx))
                (set! this.y (+ this.y this.dy)))

            (method (bounced p1 p2 maxY)
                (define good false)
                (when (or (< this.y 0) (>= this.y maxY))
                    (set! this.dy (* this.dy -1))
                    (set! good true))
                (set! good (or good (p1.bounce this) (p2.bounce this)))
                good))

        (define (prePlay delta)
            (if (< delta 1000)
                (set! statusBox.innerHTML "Ready...")
                (if (< delta 2000)
                    (set! statusBox.innerHTML "Set...")
                    (if (< delta 3000)
                        (set! statusBox.innerHTML "GO!")
                        (begin
                            (set! statusBox.style.display "none")
                            (set! state updateGame)))))
            (>= delta 3000))

        (define (updateGame delta)
            (define i 0)
            (define scoringPlayer null)
            (while (and (< i delta) (= state updateGame))
                (ball.advance)
                (when (not (ball.bounced p1 p2 (- PLAY_HEIGHT LINE_WIDTH)))
                    (if (< ball.x p1.x)
                        (set! scoringPlayer p2)
                        (when (> ball.x p2.x)
                            (set! scoringPlayer p1)))

                    (when (!= scoringPlayer null)
                        (scoringPlayer.scored)
                        (ball.drop (or (and (= scoringPlayer p1) 1) -1) (+ p1.score p2.score))
                        (set! statusBox.style.display "block")
                        (if (< scoringPlayer.score 5)
                            (set! state prePlay)
                            (set! state gameOver))))
                (set! i (+ 1 i)))
            (set! i 0)
            (while (< i delta)
                (p2.AI ball)
                (set! i (+ i AI_STEP)))
            (set! g.fillStyle "#000000")
            (g.fillRect 0 0 PLAY_WIDTH PLAY_HEIGHT)
            (set! g.fillStyle "#00FF00")
            (ball.draw)
            (p1.draw)
            (p2.draw)
            true)

        (define (gameOver delta)
            (define ret false)
            (if (< delta 3000)
                (set! statusBox.innerHTML "Game Over")
                (begin
                    (clearInterval timer)
                    (set! statusBox.style.display "none")
                    (set! startButton.style.display "block")
                    (p1.reset)
                    (p2.reset)
                    (set! state prePlay)
                    (set! ret true)))
            ret)

        (define (timerTick)
            (define currentFrame ((new Date).getTime))
            (define delta (- currentFrame lastFrame))
            (when (state delta)
                (set! lastFrame currentFrame)))

        (define (start)
            (set! startButton.style.display "none")
            (set! statusBox.style.display "block")
            (set! lastFrame ((new Date).getTime))
            (ball.drop 1 0);
            (set! state prePlay)
            (set! timer (setInterval timerTick 33)))

        (startButton.addEventListener "click" start)
        (set! ball (new Ball LINE_WIDTH LINE_WIDTH))
        (set! p1 (new Player (document.getElementById "score1")
                             LINE_WIDTH
                             (* 0.5 (- PLAY_HEIGHT PADDLE_LENGTH))
                             LINE_WIDTH
                             PADDLE_LENGTH))
        (set! p2 (new Player (document.getElementById "score2")
                             (- PLAY_WIDTH (* 2 LINE_WIDTH))
                             (* 0.5 (- PLAY_HEIGHT PADDLE_LENGTH))
                             LINE_WIDTH
                             PADDLE_LENGTH))
        (document.addEventListener "mousemove" (lambda (evt)
            (p1.moveTo (- evt.clientY (* 0.5 PADDLE_LENGTH)))))
    </script>
</head>
<body onload="loosp2js()">
    <form>
        <div id="score1" class="score">0</div>
        <div id="score2" class="score">0</div>
        <canvas id="board"></canvas>
        <div id="divider"></div>
        <div id="status" class="score">Ready</div>
        <button type="button" id="start">Click here to start...</button>
    </form>
</body>
</html>
